# -*- snakemake -*-
import os
from snakemakelib.config import update_snakemake_config
from snakemakelib.bio.ngs.db import index

include: '../settings.rules'
include: '../tools/samtools.rules'

config_default = { 
    'bio.ngs.align.bwa' : {
        'cmd' : "bwa",
        'index' : "",
        'threads' : config['bio.ngs.settings']['threads'],
        'options' : "-M",
        'mem' :{
            'options' : "",
        },
    },
}

config = update_snakemake_config(config, config_default)

config_default['bio.ngs.align.bwa']['mem']['options'] = config['bio.ngs.align.bwa']['options']

config = update_snakemake_config(config, config_default)

# Update index
config['bio.ngs.align.bwa']['index'] = index(
    ref = config['bio.ngs.align.bwa']['ref'],
    index = config['bio.ngs.align.bwa']['index'],
    application = 'bwa',
    build = config['bio.ngs.settings']['db']['build'])


bwa_cfg = config['bio.ngs.align.bwa']
ngs_cfg = config['bio.ngs.settings']
samtools_cfg = config['bio.ngs.tools.samtools']

rule bwa_mem:
    """Run bwa mem"""
    params: options = bwa_cfg['mem']['options'],
            cmd = bwa_cfg['cmd'],
            index = bwa_cfg['index']
    input: "{prefix}" + ngs_cfg['read1_label'] + ngs_cfg['fastq_suffix'],\
    "{prefix}" + ngs_cfg['read2_label'] + ngs_cfg['fastq_suffix']
    output: "{prefix}.bam"
    threads: bwa_cfg['threads']
    shell: "{params.cmd} mem -t {threads} {params.options} {params.index} {input} | " + samtools_cfg['cmd'] + " view -Sb - > {output}"
